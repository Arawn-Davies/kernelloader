offset = 0x80000000;
phys = 0x1000;

MEMORY
{
	/* At the end of the memory region is the string with the PS2 model number stored (32 Byte).
	 * Therfore the size is 0xF000 - 32
	 */
	mem(RWX) : ORIGIN = 0x80001000, LENGTH = 0xEFE0
}

PHDRS
{
	text PT_LOAD;
}

ENTRY(sbios_header)
SECTIONS {
	. = phys + offset;

	.header : AT(phys + offset) {
		*(.header)
	} > mem : text

	.text : {
  		. = ALIGN(32 / 8);
		*(.text .stub .text.* .gnu.linkonce.t.*)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
		*(.mips16.fn.*) *(.mips16.call.*)
	} > mem : text

	.reginfo : {
		*(.reginfo)
	} > mem : text

	.rodata : {
		*(.rodata .rodata.* .gnu.linkonce.r.*)
	} > mem : text

	.data : {
		*(.data .data.* .gnu.linkonce.d.*)
	} > mem : text

	.sdata : {
		*(.sdata .sdata.* .gnu.linkonce.s.*)
	} > mem : text

	.sbss : {
		*(.dynsbss)
		*(.sbss .sbss.* .gnu.linkonce.sb.*)
		*(.scommon)
	} > mem : text

	.bss : {
		*(.bss)
		. = ALIGN(32 / 8);
	} > mem : text
	
	/* Only listed to get it not interfere with above sections: */
	/DISCARD/ : {
		*(.note*)
		*(.stab*)
		*(.comment*)
		*(.mdebug*)
		*(.debug*)
	}
}

